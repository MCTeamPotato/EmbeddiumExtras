import net.darkhax.curseforgegradle.TaskPublishCurseForge
import net.darkhax.curseforgegradle.UploadArtifact

buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        maven {
            name 'parchment'
            url 'https://maven.parchmentmc.org'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: "${forgegradle_version}", changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: "${mixingradle_version}"
        classpath 'org.parchmentmc:librarian:1.+'
    }
}

plugins {
    id 'com.modrinth.minotaur' version '2.+'
    id 'net.darkhax.curseforgegradle' version '1.1.+'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'org.spongepowered.mixin'

version = "mc${minecraft_version}_v${mod_version}"
group = "com.teampotato.${mod_id}"
archivesBaseName = "embeddiumextras"

java.toolchain.languageVersion.set(JavaLanguageVersion.of(8))

minecraft {
    mappings channel: "${mappings_channel}", version: "${mappings_version}"
    runs {
        client {
            workingDirectory project.file('run')
            arg '-mixin.config=' + String.valueOf(mod_id) + '.mixins.json'
            //jvmArgs '-XX:+UnlockCommercialFeatures' // uncomment for profiling
            property 'forge.logging.console.level', 'info'
            property 'fml.earlyprogresswindow', 'false'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                embeddiumextras {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')
            arg '-mixin.config=' + String.valueOf(mod_id) + '.mixins.json'
            property 'forge.logging.console.level', 'info'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                embeddiumextras {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property 'fml.earlyprogresswindow', 'false'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            args '--mod', String.valueOf(mod_id), '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources')
            mods {
                embeddiumextras {
                    source sourceSets.main
                }
            }
        }
    }
}

compileJava {
    options.compilerArgs = ["-Xdiags:verbose"]
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

repositories {
    maven {
        url = "https://www.cursemaven.com"
    }
    maven {
        url "https://maven.blamejared.com"
    }
}

dependencies {
    implementation 'org.jetbrains:annotations:24.0.0'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation fg.deobf("org.embeddedt:embeddium-1.16.5:0.2.17-git.6ff2f16+mc1.16.5")
    implementation fg.deobf("curse.maven:jei-238222:4371666")
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"
}

java {
    withSourcesJar()
}

jar {
    manifest {
        attributes([
                "Implementation-Title": project.name,
                "Implementation-Version": "${mod_version}",
                "Implementation-Vendor" :"vice",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "${mod_id}.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

String[] supportedGameVersions = new String[]{"${minecraft_version}"}
String currentChangeLog = file("CHANGELOG.md").getText("UTF-8");

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "embeddium-extras"
    versionNumber = "${mod_version}"
    uploadFile = jar
    gameVersions.addAll(supportedGameVersions)
    loaders = ["forge"]
    versionType = "release"
    additionalFiles = [sourcesJar]
    changelog.set(currentChangeLog)
}

tasks.register("publishCurseForge", TaskPublishCurseForge) {
    apiToken = System.getenv("CURSEFORGE_TOKEN")
    disableVersionDetection()

    UploadArtifact mainFile = upload(cf_project_id, jar)
    mainFile.displayName = "${mod_id}-${version}"
    mainFile.releaseType = "release"
    mainFile.changelog = currentChangeLog
    mainFile.changelogType = "markdown"
    mainFile.addModLoader("Forge")
    mainFile.addJavaVersion("Java 17", "Java 8")
    mainFile.addGameVersion(supportedGameVersions)

    UploadArtifact sourcesFile = mainFile.withAdditionalFile(sourcesJar)
    sourcesFile.changelog = currentChangeLog
    sourcesFile.changelogType = "markdown"
}
